plugins {
    id 'cpp-library'
    id 'maven-publish'
}

group = 'org.gradle.cpp-samples'
version = '1.9.0-SNAPSHOT'

library {
    source.from file('googletest/src/gtest-all.cc')
    publicHeaders.from file('googletest/include')
    privateHeaders.from file('googletest')
}

tasks.withType(CppCompile) {
    macros GTEST_CREATE_SHARED_LIBRARY: '1'
}

tasks.withType(LinkSharedLibrary) {
    if (targetPlatform.operatingSystem.linux) {
        linkerArgs.add('-lpthread')
    }
}

def targetPlatform = compileDebugCpp.targetPlatform
def osName = targetPlatform.operatingSystem.name.replace(" ", "").toLowerCase()
def archName = targetPlatform.architecture.name.replace(" ", "").toLowerCase()

publishing {
    repositories {
        maven {
            url = 'https://repo.gradle.org/gradle/libs-snapshots-local'
            if (project.hasProperty("gartifactoryUsername") 
                && project.hasProperty("gartifactoryPassword")) {
                credentials {
                    username = gartifactoryUsername
                    password = gartifactoryPassword
                }
            }
        }
    }
    publications.withType(MavenPublication) {
        artifactId = "${artifactId}_${osName}_${archName}_4.5"
    }
}

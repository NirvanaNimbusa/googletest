plugins {
    id 'cpp-library'
    id 'maven-publish'
}

group = 'org.gradle.cpp-samples'
version = '1.9.0-SNAPSHOT'

library {
    operatingSystems = [
        objects.named(OperatingSystemFamily, OperatingSystemFamily.WINDOWS),
        objects.named(OperatingSystemFamily, OperatingSystemFamily.LINUX),
        objects.named(OperatingSystemFamily, OperatingSystemFamily.MACOS)
    ]
    source.from file('googletest/src/gtest-all.cc')
    publicHeaders.from file('googletest/include')
    privateHeaders.from file('googletest')
}

tasks.withType(CppCompile) {
    macros.put("GTEST_CREATE_SHARED_LIBRARY", '1')
    macros.put("_GLIBCXX_USE_CXX11_ABI", '0')

    println macros
    compilerArgs.addAll toolChain.map { NativeToolChain toolChain ->
        List<String> compilerSpecificArgs = []
        if (toolChain instanceof Gcc) {
            compilerSpecificArgs << '-std=c++03'
        }
        return compilerSpecificArgs
    }
}

tasks.withType(LinkSharedLibrary) {
    linkerArgs.addAll toolChain.map { NativeToolChain toolChain ->
        List<String> compilerSpecificArgs = []
        if (toolChain instanceof Gcc) {
            compilerSpecificArgs << '-std=c++03'
        }
        return compilerSpecificArgs
    }
    linkerArgs.addAll targetPlatform.map { NativePlatform targetPlatform ->
        List<String> platformSpecificArgs = []
        if (targetPlatform.operatingSystem.linux) {
            platformSpecificArgs << '-lpthread'
        }
        return platformSpecificArgs
    }
}

def metadataFormat = org.gradle.api.internal.artifacts.ivyservice.ivyresolve.parser.ModuleMetadataParser.FORMAT_VERSION
publishing {
    project.afterEvaluate {
        publications.withType(MavenPublication) {
            artifactId = "${artifactId}_${metadataFormat}"
        }        
    }
    repositories {
        maven {
            url = 'https://repo.gradle.org/gradle/libs-snapshots-local'
            if (project.hasProperty("gartifactoryUsername") 
                && project.hasProperty("gartifactoryPassword")) {
                credentials {
                    username = gartifactoryUsername
                    password = gartifactoryPassword
                }
            }
        }
    }
}

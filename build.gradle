plugins {
    id 'cpp-library'
    id 'maven-publish'
}

group = 'org.gradle.cpp-samples'
version = '1.9.0-SNAPSHOT'

library {
    operatingSystems = [
        objects.named(OperatingSystemFamily, OperatingSystemFamily.WINDOWS),
        objects.named(OperatingSystemFamily, OperatingSystemFamily.LINUX),
        objects.named(OperatingSystemFamily, OperatingSystemFamily.MACOS)
    ]
    source.from file('googletest/src/gtest-all.cc')
    publicHeaders.from file('googletest/include')
    privateHeaders.from file('googletest')
}

tasks.withType(CppCompile) {
    macros GTEST_CREATE_SHARED_LIBRARY: '1'
    compilerArgs.addAll toolChain.map { NativeToolChain toolChain ->
        List<String> compilerSpecificArgs = []
        if (toolChain instanceof Gcc) {
            compilerSpecificArgs << '-std=c++03'
        }
        return compilerSpecificArgs
    }
}

tasks.withType(LinkSharedLibrary) {
    linkerArgs.addAll toolChain.map { NativeToolChain toolChain ->
        List<String> compilerSpecificArgs = []
        if (toolChain instanceof Gcc) {
            compilerSpecificArgs << '-std=c++03'
        }
        return compilerSpecificArgs
    }
    linkerArgs.addAll targetPlatform.map { NativePlatform targetPlatform ->
        List<String> platformSpecificArgs = []
        if (targetPlatform.operatingSystem.linux) {
            platformSpecificArgs << '-lpthread'
        }
        return platformSpecificArgs
    }
}

publishing {
    repositories {
        maven {
            url = 'https://repo.gradle.org/gradle/libs-snapshots-local'
            if (project.hasProperty("gartifactoryUsername") 
                && project.hasProperty("gartifactoryPassword")) {
                credentials {
                    username = gartifactoryUsername
                    password = gartifactoryPassword
                }
            }
        }
    }
}
